/*
 * importer 
 
 	Build Runnable Jar for Importer
 
 		defaultTasks 'shadowJar'
 *
 */

/*
    Build executable jar file with gradle shadowJar
    More info at: https://imperceptiblethoughts.com/shadow/
    
    Requires Gradle 5+
 */

	//  http://ajoberstar.org/grgit/grgit-gradle.html
	// Grgit: Generally, you should only apply the plugin to the root project of your build.
	//  Plugin adds grgit property to your build, which is an instance of Grgit.
	//     Plugin looks for .git directory in build directory and it's parents
 
buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "org.ajoberstar.grgit:grgit-gradle:4.1.1"
  }
}

plugins {
	id 'com.github.johnrengelman.shadow' version '8.1.1'
	//  moved to 'buildscript'. See Notes above	id 'org.ajoberstar.grgit' version '3.1.1'
}

apply plugin: 'java'

apply plugin: "org.ajoberstar.grgit"

java {
	sourceCompatibility = 1.8      //    defaults to currently installed JVM
	// targetCompatibility  = 1.8  //    defaults to sourceCompatibility
}

repositories {
    mavenCentral()
    
    //  For Limelight Import API
    maven {
       url 'https://raw.github.com/yeastrc/limelight-import-api/repository/'
   }
}

//  Set default task for this build.gradle file
defaultTasks 'shadowJar'


configurations.all {
	//  Exclude default logger for Spring Boot 'logback'
   exclude group:"ch.qos.logback", module:"logback-core"
   exclude group:"ch.qos.logback", module:"logback-classic"
   	//  Exclude Routing of log4j to slf4j since using opposite (slf4j to log4j) and including this would create a circular reference
   exclude group:'org.apache.logging.log4j', module: 'log4j-to-slf4j'
//   exclude group:'org.apache.logging.log4j', module: 'log4j-api'
}

//  Build 'compile' string for 'org.yeastrc:limelight-import-api:###'

 String limelight_import_version = project.file('../Gradle_limelight-import-api_version_for_Gradle.txt').getText('UTF-8');

//  This commented out variable may NOT be current/correct string.
// String limelight_import_version = "0.9.0";
 
println("limelight_import_version: " + limelight_import_version);

String limelight_import = 'org.yeastrc:limelight-import-api:' + limelight_import_version;

println("compile for 'org.yeastrc:limelight-import-api': " + limelight_import);



dependencies {

	//  Limelight Import API Jar - Need to Update Version as needed
	
	//      Pulled from REPO (entered above): url 'https://raw.github.com/yeastrc/limelight-import-api/repository/'
	
	implementation limelight_import

	//   AWS  S3
	
	// https://mvnrepository.com/artifact/software.amazon.awssdk/s3
	implementation group: 'software.amazon.awssdk', name: 's3', version: '2.20.32'
	
	// https://mvnrepository.com/artifact/software.amazon.awssdk/apache-client
	implementation group: 'software.amazon.awssdk', name: 'apache-client', version: '2.20.33'
	
	
	//   OLD V1
	// https://mvnrepository.com/artifact/com.amazonaws/aws-java-sdk-s3
	// compile group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: '1.12.416'

	

	// https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
	implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
	
	// https://mvnrepository.com/artifact/commons-io/commons-io
	implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
	
	// https://mvnrepository.com/artifact/org.apache.commons/commons-dbcp2
	implementation group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.9.0'
	
	// https://mvnrepository.com/artifact/mysql/mysql-connector-java
	implementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.28'
	
	//  Logging
	
	implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.17.2'
	implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.17.2'	
	implementation group: 'org.apache.logging.log4j', name: 'log4j-web', version: '2.17.2' //  Needed for Web app
	
	// https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-slf4j-impl
	implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.17.2'
	
	//  Jakarta Commons Logging to SLF4J
	implementation group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.7.36'
	
	//  Log4j 1 to 2 bridge
	implementation group: 'org.apache.logging.log4j', name: 'log4j-1.2-api', version: '2.17.2'
		
	// https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-core
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.13.4'
	
	// https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.4'
	

	// JAXB dependencies (Required for Java 11+)
	// https://mvnrepository.com/artifact/jakarta.xml.bind/jakarta.xml.bind-api
	implementation group: 'jakarta.xml.bind', name: 'jakarta.xml.bind-api', version: '2.3.3'
	// https://mvnrepository.com/artifact/org.glassfish.jaxb/jaxb-runtime
	implementation group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '2.3.6'
	

	//  NOT FOUND  in mavenCentral()
	
	//     Took Gradle listing from https://mvnrepository.com/artifact/jargs/jargs/1.0
    // https://mvnrepository.com/artifact/jargs/jargs
//	compile group: 'jargs', name: 'jargs', version: '1.0'

	//////////////////////////////
	
	//  Jars copied and placed here	
	implementation fileTree(dir: '../lib_other', include: ['*.jar'])
	
	//  Removed since pull the API jar another way
	//  Jars built from other Limelight Github repositories
	// compile fileTree(dir: '../libs_limelight_github_jars', include: ['*.jar'])
	
	//  Jars built from other Yeastrc Github repositories
	implementation fileTree(dir: '../libs_yeastrc_github_jars', include: ['*.jar'])
	
	
//  Specify dependency on Limelight Shared Code
	
	implementation project(":limelight_shared_code")

//  Specify dependency on limelight_importer_run_importer_shared

	implementation project(":limelight_importer_run_importer_shared")
	
}

//  Assign to grgit, created by plugin
def gitRepo = grgit


// def gitRepo = Grgit.open({
//  dir = ".."  //  Use parent directory since that is root of GIT repo
//  currentDir = <path>
//  credentials = <credentals>
// })

def gitHeadId = gitRepo.head().id
def gitCommit = gitRepo.head().getAbbreviatedId(8)
def gitBranch = gitRepo.branch.getCurrent().getName()

//  Get tag at GIT HEAD: like git tag --points-at HEAD
def repoHead = gitRepo.head()
def repoHeadTag = gitRepo.tag.list().find {
	it.commit == repoHead
}
def repoTagName = "NO GIT Tag at HEAD"

if ( repoHeadTag ) {
	println("GIT Tag at HEAD found")
	repoTagName = repoHeadTag.getName()
}


println("gitHeadId: " + gitHeadId )
println("gitCommit: " + gitCommit )
println("gitBranch: " + gitBranch )
println("GIT Tag at HEAD: " + repoTagName )

jar {
    manifest {
        attributes([ 'Main-Class': 'org.yeastrc.limelight.limelight_importer.program.LimelightImporterProgram',
        			"Multi-Release": true,
        			'Project-Title': project.name,
					'Build-Timestamp': new Date().format("yyyy-MM-dd HH:mm:ssZ"),
					'Git-Commit': gitCommit,
					'GIT-Full-HASH': gitHeadId,
					'GIT-Tag-at-HEAD': repoTagName,
					'Git-Branch': gitBranch ])
    }
}


//  NOT NEEDED:  files from 'src/main/resources' copied to resulting jar without this.

//  For creating runnable jar
//  processResources {
//    from ('src/main/resources') {
//        include '**/*'
//    }
// }


//   https://imperceptiblethoughts.com/shadow/configuration/#configuring-output-name

shadowJar {

    archiveBaseName = 'limelightImporter_XMLFile_ScanFiles'
    
    // Required to output same as previous filename. 
    //		Default is 'all' which will add '-all' to output filename before '.jar'
    
    archiveClassifier = '' 
    
    //  Default is empty string 
    archiveVersion = ''
    
    //  For Log4J2
    transform(com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer)
}
